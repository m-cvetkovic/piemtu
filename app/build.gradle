apply plugin: 'com.android.application'

int gitCalculateVersionCode() {
    try {
	def code = new ByteArrayOutputStream()
	exec {
            // Add Android_Ver git tag to the first published revision
	    commandLine 'git', 'rev-list', '--count', 'Android_Ver..HEAD'
	    standardOutput = code
	}
	return code.toString().trim().toInteger() + 1
    }
    catch (ignored) {
	return -1
    }
}

String gitCalculateVersionName(String tag_pattern) {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--long', "--match=${tag_pattern}"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "ca.on.prosperacc.piemtu"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode gitCalculateVersionCode()
        // Add r_M.N annotated git tag for each major version
        versionName gitCalculateVersionName('r_*')
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'eu.chainfire:libsuperuser:1.1.0.202004101746'
    implementation 'com.google.android.material:material:1.1.0'
}
